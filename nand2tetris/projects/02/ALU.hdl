// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   Mux16 (a[0..15]=x[0..15], b[0..15]=false, sel=zx, out[0..15]=zxOut);
   Mux16 (a[0..15]=y[0..15], b[0..15]=false, sel=zy, out[0..15]=zyOut);
   Not16 (in[0..15]=zxOut, out[0..15]=notzx);
   Not16 (in[0..15]=zyOut, out[0..15]=notzy);
   Mux16 (a[0..15]=zxOut, b[0..15]=notzx, sel=nx, out[0..15]=nxOut);
   Mux16 (a[0..15]=zyOut, b[0..15]=notzy, sel=ny, out[0..15]=nyOut); 
   Add16 (a[0..15]=nxOut, b[0..15]=nyOut, out[0..15]=addOut);
   And16 (a[0..15]=nxOut, b[0..15]=nyOut, out[0..15]=andOut);
   Mux16 (a[0..15]=andOut, b[0..15]=addOut, sel=f, out[0..15]=fOut);
   Not16 (in[0..15]=fOut, out[0..15]=notfOut);
   Mux16 (a[0..15]=fOut, b[0..15]=notfOut, sel=no, out[0..7]=lowOut, out[8..15]=highOut,
   		out[15]=ng, out[0..15]=out[0..15]);
   Or8Way (in=lowOut, out=orOutA);
   Or8Way (in=highOut, out=orOutB);
   Or (a=orOutA, b=orOutB, out=finalOr);
   Not (in=finalOr, out=zr);
   

  

}
